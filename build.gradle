plugins {
    id 'jacoco'
    id 'java'
    id 'application'
}

repositories {
    jcenter()
}

version = 1.12
sourceCompatibility = JavaVersion.VERSION_1_8 
targetCompatibility = JavaVersion.VERSION_1_8
 
mainClassName = 'Main'
ext.appdir = file(System.env.SAM_PROGRAMS+'/backup/backup.beta/bin')

ext.JUNIT_VERSION = '5.3.2'
ext.TRUTH_VERSION = '0.42'

dependencies {
  implementation files('myutils.jar')
  implementation 'org.codejargon.feather:feather:+'
  implementation 'org.apache.logging.log4j:log4j-api:2.11.2'
  implementation 'org.apache.logging.log4j:log4j-core:2.11.2'
  implementation 'org.json:json:20180813'
  
  testImplementation 'com.thedeanda:lorem:2.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:'+JUNIT_VERSION
  
  // testImplementation 'org.junit.jupiter:junit-jupiter-params:'+JUNIT_VERSION
   testRuntime 'org.junit.jupiter:junit-jupiter-engine:'+JUNIT_VERSION
   
  // testImplementation 'com.google.truth:truth:'+TRUTH_VERSION
  //  testImplementation 'com.google.truth.extensions:truth-java8-extension:'+TRUTH_VERSION
}
	
task combineCss {
  def rsrc = sourceSets.main.resources.srcDirs[0] 
  ext.rsrcDir = new File(rsrc, 'styles')
  ext.outputFile = new File(rsrc, 'styles.css')
  
  doLast {
    long mod  = outputFile.lastModified();
    def filesList = file(rsrcDir).listFiles();
    if(filesList.any{it.lastModified() != mod}) {
      file(outputFile).text = 
        filesList
		.collect{f -> "/** ${f.name} */ \n"+f.text+'\n\n'}
		.join()
	  
	  mod = outputFile.lastModified();
	  filesList.each{it.lastModified = mod}
	  println('css combined');
    }
  
  }
}

compileJava.dependsOn << combineCss

run {
 main 'Main'
 workingDir appdir.parent
}

task deleteJars {
  doLast {
     file(appdir).listFiles().each({
       if(it.name.endsWith('.jar'))
         it.delete();
     });
  }
}

task install(type: Copy, dependsOn:[installDist, deleteJars]) {
  def libdir = file(new File(installDist.destinationDir, 'lib')) 
  from libdir
  into appdir
  
  doLast {
     new File(appdir, 'run.cmd').text = """
  @echo off
  setlocal
  
  ${versionText()}
  
  java %JVM_OPTS% %JAVA_OPTS% -cp \"%~dp0;%~dp0${String.join(';%~dp0', libdir.list())}\" ${mainClassName} %*
  
  """.stripIndent()
  } 
}

def versionText() {
  if(!version && version == 'unspecified') return '';
  return """
  SET APP_VERSION=${version}
  if [%1]==[-v] (
    echo version: %APP_VERSION%
    goto:eof
  )
  if [%1]==[--version] (
    echo version: %APP_VERSION%
    goto:eof
  )
  """;    
}

jar {
  exclude 'styles'
}

defaultTasks 'install'


test {
  useJUnitPlatform()
  systemProperty 'java.util.logging.config.file','test-logging.properties'
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacoco.html")
    }
}

